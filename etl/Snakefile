configfile: "config.yml"


#
# Import our dataset specific rules
#
module aqueduct:
    snakefile: "pipelines/aqueduct/rules.smk"
use rule * from aqueduct as aqueduct_*


module dem:
    snakefile: "pipelines/dem/rules.smk"
    config: config
use rule * from dem as dem_*
ruleorder: dem_clip_raster > clip_raster


module exposure_nature:
    snakefile: "pipelines/exposure_nature/rules.smk"
use rule * from exposure_nature as exposure_nature_*


# gem_earthquake has no dataset specific rules defined


module ghsl_buildings:
    snakefile: "pipelines/ghsl_buildings/rules.smk"
    config: config
use rule * from ghsl_buildings as ghsl_buildings_*


module iris:
    snakefile: "pipelines/iris/rules.smk"
use rule * from iris as iris_*


module isimip:
    snakefile: "pipelines/isimip/rules.smk"
use rule * from isimip as isimip_*


module jrc_pop:
    snakefile: "pipelines/jrc_pop/rules.smk"
    config: config
use rule * from jrc_pop as jrc_pop_*


module land_cover:
    snakefile: "pipelines/land_cover/rules.smk"
use rule * from land_cover as land_cover_*
ruleorder: land_cover_download_300m_2020_from_CDS > clip_raster
ruleorder: land_cover_ingest_categorical_raster > ingest_rasters


module storm:
    snakefile: "pipelines/storm/rules.smk"
use rule * from storm as storm_*


module traveltime_to_healthcare:
    snakefile: "pipelines/traveltime_to_healthcare/rules.smk"
use rule * from traveltime_to_healthcare as traveltime_to_healthcare_*


#
# Import rule definitions common to many datasets
#
include: "rules/raster_processing.smk"
include: "rules/database.smk"


# Datasets the `all` target rule will expand to
ALL_DATASETS = (
    "aqueduct",
    "land_cover",
    "exposure_nature",
    "gem_earthquake",
    "ghsl_buildings",
    "iris",
    "isimip",
    "jrc_pop",
    "storm",
    "traveltime_to_healthcare",
    "dem",
)

# prefix any shell blocks with these statements
# - create variables from env file and export
# - print commands to stdout during execution
# - exit if any command has non-zero exit status, not just the last in a rule
shell.prefix(f"set -a && source ../envs/{config['environment']}/.etl.env && set +a; set -x; set -e; ")

wildcard_constraints:
    KEY="[^/]+",
    DATASET="[^/]+",
    EPOCH="\d+",
    RELEASE="\d{4}[A-Z]",
    RP="\d+",
    SSP="constant|ssp\d",

rule all:
    """
    Target rule to fetch, process and ingest all registered datasets.
    """
    input:
        lambda x: expand("raster/metadata/{dataset}.flag", dataset=ALL_DATASETS)
